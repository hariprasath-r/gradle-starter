buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"
    }
}

/**
 * Created a new custom plugin
 * We can specify Project/Task/Setting in the interface type
 * Inside the apply method, we can specify the tasks to execute
 */
public class MyAWSPlugin implements Plugin<Project> {

    @Override
    void apply(Project project) {

        project.task('copyToS3') {
            doLast { println 'Copied to S3' }
        }

        project.task('deployToEC2') {
            doLast {
                println 'Application deployed to EC2, should be up and running in few mins...'
            }
        }

    }
}

apply plugin: MyAWSPlugin
apply plugin: 'org.sonarqube'
sonarqube {
    properties {
        property 'sonar.login', 'admin'
        property 'sonar.password', '123456'
//        property "sonar.projectKey", "projectKey"
//        property "sonar.organization", "org"
//        property "sonar.host.url", "https://sonarcloud.io"
//        property "sonar.verbose", "true"
    }
}

// allprojects method of Project class is used to apply tasks to all projects including parent and child
allprojects {
    task printProjectNames {
        doLast {
            println project.name
        }
    }
}

// subprojects method of Project class is used to apply to all sub projects
subprojects {
    version '1.0.0'
    group 'in.hp.gradle'

    task printSubProjectNames {
        doLast {
            println project.name
        }
    }

    // when using plugins inside subprojects, use apply method
    // apply method accepts map
    apply plugin: 'application'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation group: 'junit', name: 'junit', version: '4.13.2'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.10.0'
    }

    /**
     * by default, test and jTR tasks are independent
     * to make them run one after another, we are making them dependent on each other
     */
    test.finalizedBy(jacocoTestReport)
    jacocoTestReport.dependsOn test
}

// child project level dependencies can be specified in root
project('webapp') {
    dependencies {
        // mentioning that webapp is dependent on services
        implementation project(':services')
    }
}

project('services') {
    dependencies {
        implementation project(':data')
        implementation project(':integration')
    }
}
